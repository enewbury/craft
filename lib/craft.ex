#
# commands and queries are handled differently
#
# commands modify state, so they're automatically directed to the leader to go through a consensus round
#
# queries read state, their handling depends on the level of consistency desired:
#   - linearizable queries are always directed to the leader
#     - if leader leases are enabled (you're using an accurate time source), the leader's machine can immediately process the request
#     - otherwise, we need to wait for a round of consenus to occur to confirm that the leader is still the leader
#       - when a client makes a query, the machine process simply buffers it
#       - when the next consensus round achieves consensus, the consensus process informs the machine process of the (possibly bumped) commit index
#       - the machine process flushes the buffer and processes all waiting queries
#
#  - eventually consistent queries are immediately processed by the receiving node (random node or one provided by the user)
#    - if the user consistently uses the same node, monotonic read consistency is supported
#    - otherwise, if the user leaves it up to craft to pick a random node, there are no consistency guarantees, state can be arbitrarily stale, and even violate causality
#
defmodule Craft do

  # FIXME
  @type group_name :: any()
  @type command :: any()
  @type reply :: any()
  @type side_effects :: any()
  @type query() :: any()
  @type log_index() :: non_neg_integer()

  def backend do
    case Application.get_env(:craft, :backend, __MODULE__.Raft) do
      {module, _args} ->
        module

      module when is_atom(module) and not is_nil(module) ->
        module
    end
  end

  @doc """
  Starts a raft group on a list of erlang nodes.

  `nodes` must be an accessible list of erlang nodes, and
  `machine` must be a module that uses `Craft.Machine` for functionality and implements its behaviour.

  ### Opts
    - `:persistence` - Configure how the log is persisted with a {module, args} tuple where module aheres to the `Craft.Persistence` behaviour
  """
  def start_group(name, nodes, machine, opts \\ []), do: backend().start_group(name, nodes, machine, opts)

  @doc "Stops all members of the group"
  def stop_group(name), do: backend().stop_group(name)

  @doc "Starts the local member with the given name"
  def start_member(name), do: backend().start_member(name)

  @doc "Stops the local member with the given name"
  def stop_member(name), do: backend().stop_member(name)

  @doc """
  Adds a node to an existing group.

  ### Opts
  - `timeout` - (default: 5_000) the time before we return a timeout error
  """
  def add_member(name, node, opts \\ []), do: backend().add_member(name, node, opts)

  @doc """
  Removes a node from membership in the group, but doesn't stop its processes.

  ### Opts
  - `timeout` - (default: 5_000) the time before we return a timeout error
  """
  def remove_member(name, node, opts \\ []), do: backend().remove_member(name, node, opts)

  @doc "Selects a new node to become the leader of the group."
  def transfer_leadership(name, to_node), do: backend().transfer_leadership(name, to_node)

  @doc "Transfers leadership to a random follower."
  def transfer_leadership(name), do: backend().transfer_leadership(name)

  @doc "Writes a backup of the local member of the given group to the provided path."
  def backup(name, path), do: backend().backup(name, path)

  @doc "Copies the backup at the given path to Craft's data directory. You must call Craft.start_member/1 afterwards."
  def restore(path), do: backend().restore(path)

  @doc """
  Sends a message to the local instance of the user's state machine.

  Receivable by the `handle_info/2` callback.
  """
  def send(name, message), do: backend().send(name, message)

  @doc "Responds to an asynchronous query, akin to GenServer.reply/2"
  def reply(meta, reply), do: backend().reply(meta, reply)

  @doc "Generates `ref` and `from` values for use in testing async queries in a sandboxed user machine that calls `reply/2`."
  def reply_from, do: backend().reply_from()

  @doc "Initializes the MemberCache for the group with the given nodes"
  def discover(name, nodes), do: backend().discover(name, nodes)

  @doc """
  Indicates, for the given group, if this node holds the lease.

  This function is intended to extend craft's linearizabiliy guarantees to processes outside of the user's state machine when used with leader_ready?/1.
  """
  def holding_lease?(name), do: backend().holding_lease?(name)

  @doc "See holding_lease?/1, called from within a user's machine callback"
  def holding_lease?(), do: backend().holding_lease?()

  @doc """
  Indicates if the user's state machine on the leader has caught up after an election.

  This function is intended to extend craft's linearizabiliy guarantees to processes outside of the user's state machine when used with holding_lease?/1.
  """
  def leader_ready?(name), do: backend().leader_ready?(name)

  @doc "Lists the groups known to this node, with cached information (members, lease holder, etc..)"
  def known_groups(), do: backend().known_groups()

  @doc "Lists cached information about the given group."
  def cached_info(group_name), do: backend().cached_info(group_name)

  @doc "Destroys all local data for the given group."
  def purge(name), do: backend().purge(name)

  @doc "Gives the current time, if the machine has a global clock configured. Called from within the user's machine callback."
  def now(), do: backend().now()

  @doc """
  Submits a command to the given group `name`, once quorum is reached, the command is executed.

  In any distributed system, it's possible to submit a command and not hear the response. This leaves the client in a state where
  it doesn't know if the command was committed by the system or not. Because of this, Craft returns the following error shapes:

  `{:error, reason}`
    - Craft knows the command has not, and will not ever execute.
  `{:error, reason, %{request_id: request_id}}`
    - Craft doesn't know if the command committed, you can then call `Craft.command_status/1` with the given `request_id` to find out.
    - If the given `reason` is one you're expecting (it's from your application state machine), the command has executed and returned the response you asked it to.

  See `c:Machine.handle_command/3`.

  ### Opts
  - `timeout` - (default: 5_000) the time before we return a timeout error
  """
  def command(command, name, opts \\ []), do: backend().command(command, name, opts)

  @doc """
  Asynchronous version of `command/3`, returns a unique `request_id`, then sends a message of the form `{:"$craft_command", request_id, reply}` with the reply to the caller when the command has executed.

  If the caller hasn't heard back from the leader by the time the specified timeout is hit, a message of the form `{:"$craft_command", request_id, {:error, :timeout, metadata}}` will be sent to the
  caller. It's still possible that the result of the command could arrive after that.

  The given `request_id` may be used with `command_status/3`, if desired.
  """
  def async_command(command, name, opts \\ []), do: backend().async_command(command, name, opts)

  @doc """
  Returns the status of the command associated with the given `request_id`, see `command/3`.

  Gives one of the following values:

  - `:committed` - the command has committed
  - `:uncommitted` - the command was found in the leader's log, but has not yet committed
  - `:unknown`
    - the command was not found in the leader's log, the command may have committed but the log has been expunged due to a snapshot.

  ### Opts
  - `timeout` - (default: 5_000) the time before we return a timeout error
  """
  def command_status(name, request_id, opts \\ []), do: backend().command_status(name, request_id, opts)

  #
  # Craft.query(command, name, consistency: :linearizable)
  # if `consistency` is `:linearizable`, will address leader
  # if `consistency` is `{:linearizable, node}`, will address given node
  # if `consistency` is `:eventual`, will address random follower
  # if `consistency` is `{:eventual, node}`, will address given node
  #
  # Craft.command(command, name) # always goes to leader, since it can modify state

  @doc """
  Runs a read-only query against the machine state without committing a log message.

  Depending on the `consistency` option's value, queries can access other nodes than the leader to
  spread load. This is in contrast to a query that always addresses the leader since it can modify state.

  ### Opts
  - `:consistency` - Configures the type of consistency guarentees for the query
  - `timeout` - (default: 5_000) the time before we return a timeout error

  ### Consistency values
  - `:linearizable` - query is run on the leader (default)
  - `{:linearizable, {:node, node}}` - query is run on the given node as a linearizable read-index query
    - this incurs one extra network RTT (client -> follower -> leader), as the follower needs to get the latest commit index from the leader
        - if leases are enabled, the leader can immediately return its commit index
        - if leases are disabled, the leader must go through a quorum round to return the commit index (one additional RTT)
    - this allows offloading heavy queries to followers without compromising safety, but at the expense of latency. it's quite useful for rebuilding volatile local caches after a deploy.
  - `:eventual` - query is run on a random node
  - `{:eventual, :leader}` - query is run on the leader without checking for quorum before returning result
  - `{:eventual, {:node, node}}` - query is run on the given node without linearizable guarentees
  """
  def query(query, name, opts \\ []), do: backend().query(query, name, opts)

  @doc "Requests a different leader than the current."
  def step_down(name), do: backend().step_down(name)

  @doc """
    Switches all machines in the given group `name` to the specified `mode`.

    Allowed modes are: :normal | :write_optimized

    In :write_optimized mode, the machine defers execution of commands until they're needed, or the system enters a quiescent period.
    This mode is best for write-heavy workloads or for periods of bulk ingestion.

    - ALL commands will return `:ok`, regardless of the reply provided by the user's machine.
    - You must use `Craft.leader_ready?/1` in non-Craft components to ensure linearizability

    This mode takes the user's state machine out of the write path and makes linearizable reads just-in-time. It essentially turns Craft into a write-ahead log, with latent command execution,
    hence why commands may only return `:ok`, indicating that replication via quorum has taken place, but not command execution.

    A number of events will cause craft to process all outstanding comands:

      - A client requests a linearizable query from the leader. This is necessary to ensure correctness guarantees.
      - An election takes place, the new leader will execute all outstanding commands before assuming leadership (to preserve section 5.4.2 correctness).
      - The maximum log size is reached, configurable via the `[:craft, :maximum_log_length]` config key.
  """
  def switch_mode(name, mode) when mode in [:normal, :write_optimized], do: backend().switch_mode(name, mode)

  @persistence_functions [
     :commit_buffer,
     :fetch,
     :fetch_between,
     :fetch_from,
     :reverse_find,
     :rewind,
     :truncate
   ]

  @heartbeat_reply_warnings [
     :duplicate,
     :missed_deadline,
     :out_of_order,
     :round_expired
   ]

  @roles [
     :lonely,
     :receiving_snapshot,
     :follower,
     :candidate,
     :leader
   ]

  @messages [
     :append_entries,
     :append_entries_results,
     :request_vote,
     :request_vote_results,
     :install_snapshot,
     :install_snapshot_results
   ]

  @user_machine_callbacks [
     :handle_command,
     :handle_commands,
     :handle_query,
     :snapshot
   ]

  @events [
     [:craft, :quorum, :heartbeat],
     [:craft, :quorum, :succeeded],
     [:craft, :check_quorum, :succeeded],
     [:craft, :check_quorum, :failed]
   ]

  def telemetry_events do
    heartbeat_reply_warning_events = Enum.map(@heartbeat_reply_warnings, fn warning -> [:craft, :heartbeat, :reply, warning] end)
    persistence_events = Enum.map(@persistence_functions, fn function -> [:craft, :persistence, function] end)
    role_change_events = Enum.map(@roles, fn role -> [:craft, :role, role] end)
    message_sent_events = Enum.map(@messages, fn message -> [:craft, :message, :sent, message] end)
    user_machine_callback_events = Enum.map(@user_machine_callbacks, fn callback -> [:craft, :machine, :user, callback] end)

    @events ++ persistence_events ++ heartbeat_reply_warning_events ++ role_change_events ++ message_sent_events ++ user_machine_callback_events
  end

  @doc false
  def state(name, node), do: backend().state(name, node)

  @doc false
  def state(name), do: backend().state(name)
end
