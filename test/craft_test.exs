defmodule CraftTest do
  use Craft.NexusCase,
      parameterize: (for leases <- [true, false], do: %{leader_leases: leases})

  alias Craft.Nexus.Stability
  alias Craft.SimpleMachine

  nexus_test "starts a group, elects a leader, replicates logs, processes commands", %{leader_leases: leases} = ctx  do
    %{name: name, nexus: nexus} = ctx
    wait_until(nexus, {Stability, :all})

    if leases do
      # wait out lease, TODO: `wait_until(nexus, :leader_holds_lease)`
      Process.sleep(2_000)
    end

    assert :ok = SimpleMachine.put(name, :a, 123)
    assert {:ok, 123} = SimpleMachine.get(name, :a)

    assert {:ok, request_id} = SimpleMachine.put_async(name, :b, 456)
    assert_receive {:"$craft_command", ^request_id, :ok}
  end

  describe "command/2 + command_status/2" do
    nexus_test "committed command", %{name: name, nexus: nexus} do
      wait_until(nexus, {Stability, :all})

      assert {:error, :some_error, %{request_id: request_id}} = SimpleMachine.return_an_error(name, :some_error)

      assert :committed == Craft.command_status(name, request_id)
    end

    nexus_test "uncommitted command", %{name: name, nexus: nexus} do
      wait_until(nexus, {Stability, :all})

      # prevent quorum
      nemesis(nexus, fn _ -> :drop end)

      # allow enough time to write to the leader's log before timing out
      assert {:error, :timeout, %{request_id: request_id}} = SimpleMachine.put(name, :a, :b, timeout: 50)

      assert :uncommitted == Craft.command_status(name, request_id)
    end

    nexus_test "unknown command", %{name: name, nexus: nexus} do
      wait_until(nexus, {Stability, :all})

      # prevent quorum
      nemesis(nexus, fn _ -> :drop end)

      # timeout immediately to beat the leader to writing the log entry
      assert {:error, :timeout, %{request_id: request_id}} = SimpleMachine.put(name, :a, :b, timeout: 0)

      assert :unknown == Craft.command_status(name, request_id)
    end
  end

  nexus_test "async_command/3", %{name: name, nexus: nexus} do
    wait_until(nexus, {Stability, :all})

    assert {:ok, request_id} = SimpleMachine.put_async(name, :a, 123)
    assert_receive {:"$craft_command", ^request_id, :ok}
    assert {:ok, 123} = SimpleMachine.get(name, :a)

    assert {:error, :timeout, _} = SimpleMachine.put_async(name, :a, 456, timeout: 0)
    assert {:ok, request_id} = SimpleMachine.put_async(name, :a, 456, timeout: 10)
    assert_receive {:"$craft_command", ^request_id, {:error, :timeout, _}}
  end

  describe "query/3" do
    nexus_test "exits early when timeout is hit", %{name: name, nexus: nexus, leader_leases: leases} do
      wait_until(nexus, {Stability, :all})

      nemesis(nexus, fn _ -> :drop end)
      if not leases do
        # assert timeout when no network (leader can't get quorum for read)
        assert {:error, :timeout} = Craft.query({:get, :a}, name, timeout: 1)
      end
    end

    nexus_test "consistency setting affects availability", %{name: name, nodes: nodes, nexus: nexus} do
      %{leader: leader} = wait_until(nexus, {Stability, :all})
      # assert leader doesn't need to contact other nodes in eventual mode
      assert {:ok, nil} = Craft.query({:get, :a}, name, consistency: {:eventual, :leader})
      assert {:ok, nil} = Craft.query({:get_parallel, :a, []}, name, consistency: {:eventual, :leader})

      # assert returns out of date value on isolated node in :eventual mode
      isolated_node = Enum.random(nodes -- [leader])

      nemesis(nexus, fn
        {_, from, to, _} when isolated_node in [to, from] -> :drop
        _event -> :forward
      end)

      assert :ok = SimpleMachine.put(name, :a, 123)
      assert {:ok, 123} = Craft.query({:get, :a}, name)

      assert {:ok, nil} = Craft.query({:get, :a}, name, consistency: {:eventual, {:node, isolated_node}})
      assert {:ok, nil} = Craft.query({:get_parallel, :a, []}, name, consistency: {:eventual, {:node, isolated_node}})

      # assert successful linearizable query when network returns
      nemesis(nexus, fn _ -> :forward end)
      assert {:ok, 123} = Craft.query({:get, :a}, name, consistency: :linearizable)
    end
  end

  nexus_test "leadership transfer", %{nodes: nodes, name: name, nexus: nexus} do
    %{leader: leader} = wait_until(nexus, {Stability, :all})

    new_leader = Enum.random(nodes -- [leader])
    Craft.transfer_leadership(name, new_leader)

    assert %{leader: ^new_leader} = wait_until(nexus, {Stability, :all})
  end

  nexus_test "remove a member", %{nodes: nodes, name: name, nexus: nexus} do
    %{leader: leader} = wait_until(nexus, {Stability, :all})

    member = Enum.random(nodes -- [leader])
    :ok = Craft.remove_member(name, member)
    members = Craft.state(name) |> Map.keys()
    assert members == nodes -- [member]

    assert {:error, :unknown_member} = Craft.remove_member(name, member)
  end

  nexus_test "new member", %{nodes: nodes, name: name, nexus: nexus} do
    wait_until(nexus, {Stability, :all})

    [new_node] = Craft.TestCluster.spawn_nodes(1)
    :ok = Craft.add_member(name, new_node)

    wait_until(nexus, {Stability, :all})

    members = name |> Craft.state() |> Map.keys() |> MapSet.new()
    assert members == MapSet.new([new_node | nodes])

    assert {:error, :already_joined} = Craft.add_member(name, new_node)

    # already started node
    :ok = Craft.remove_member(name, new_node)
    :ok = Craft.add_member(name, new_node)

    wait_until(nexus, {Stability, :all})

    members = name |> Craft.state() |> Map.keys() |> MapSet.new()
    assert members == MapSet.new([new_node | nodes])
  end

  nexus_test "handle_role_change/2", %{name: name, nodes: nodes, nexus: nexus} do
    wait_until(nexus, {Stability, :all})

    ref = make_ref()
    :ok = SimpleMachine.put(name, :test_process_from, {self(), ref})

    Process.sleep(5_000)
    %{leader: leader} = wait_until(nexus, {Stability, :all})

    new_leader = Enum.random(nodes -- [leader])
    Craft.transfer_leadership(name, new_leader)

    assert_receive {^ref, {:role_change, ^new_leader, :candidate}}
    assert_receive {^ref, {:role_change, ^new_leader, :leader}}
  end

  nexus_test "Craft.send/2 + handle_info/2", %{name: name, nodes: nodes, nexus: nexus} do
    wait_until(nexus, {Stability, :all})

    message = make_ref()
    :ok = :rpc.call(Enum.random(nodes), Craft, :send, [name, {self(), message}])

    assert_receive ^message
  end

  nexus_test "backup/2 + purge/1 + restore/1 roundtrip", %{name: name, nodes: nodes, nexus: nexus} do
    wait_until(nexus, {Stability, :all})

    ref = make_ref()
    :ok = SimpleMachine.put(name, :test_key, ref)

    wait_until(nexus, {Stability, :all})

    backup_node = Enum.random(nodes)
    backup_dir = Path.join([
                   System.tmp_dir!(),
                   "craft_test_backup_" <> (:crypto.strong_rand_bytes(8) |> Base.encode16())
                 ])

    :ok = :rpc.call(backup_node, Craft, :backup, [name, backup_dir])

    :ok = Craft.stop_group(name)

    for node <- nodes do
      assert is_list(:rpc.call(node, Craft, :purge, [name]))

      :ok = :rpc.call(node, Craft, :restore, [backup_dir])
      {:ok, _pid} = :rpc.call(node, Craft.MemberSupervisor, :start_existing_member, [name, %{nexus_pid: nexus}])
    end

    wait_until(nexus, {Stability, :majority})

    assert {:ok, ^ref} = SimpleMachine.get(name, :test_key)

    File.rm_rf!(backup_dir)
  end
end
